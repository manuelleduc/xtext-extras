/*******************************************************************************
 * Copyright (c) 2011 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.mbase.annotations.MbaseWithAnnotations with org.eclipse.xtext.mbase.Mbase

import "http://www.eclipse.org/xtext/mbase/Mbase" as mbase
import "http://www.eclipse.org/xtext/mbase/Xtype" as xtype
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/Xtext/mbase/XAnnotations"

XAnnotation :
	{XAnnotation}
	'@' annotationType=[types::JvmAnnotationType | QualifiedName] (=>'('
		(
			elementValuePairs+=XAnnotationElementValuePair (',' elementValuePairs+=XAnnotationElementValuePair)*
		|	value=XAnnotationElementValueOrCommaList
		)?
	')')?
;

XAnnotationElementValuePair :
	=>(element=[types::JvmOperation|ValidID] '=') value=XAnnotationElementValue
;

XAnnotationElementValueOrCommaList returns mbase::XExpression:
	  =>({mbase::XListLiteral} '#' '[') (elements+=XAnnotationOrExpression (',' elements+=XAnnotationOrExpression )*)? ']'
	| XAnnotationOrExpression ({mbase::XListLiteral.elements+=current} (',' elements+=XAnnotationOrExpression )+)?
;

XAnnotationElementValue returns mbase::XExpression :
	  =>({mbase::XListLiteral} '#' '[') (elements+=XAnnotationOrExpression (',' elements+=XAnnotationOrExpression )*)? ']'
	| XAnnotationOrExpression
;

XAnnotationOrExpression returns mbase::XExpression:
	XAnnotation |
	XExpression
;

